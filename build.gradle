plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'me.mohamad82.pensieve'
version = '1.0'
description = 'A well-supported recording and replaying system for Minecraft'
compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }
    maven {
        url = uri("https://maven.pkg.github.com/mohamad82bz/ruom")
        credentials {
            username = project.findProperty("gpr.user")
            password = project.findProperty("gpr.key")
        }
    }
    maven {
        url = uri('https://repo.screamingsandals.org/public/')
    }
    maven {
        url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/')
    }
}

dependencies {
    implementation 'me.mohamad82:ruom-bukkit:4.10.11'
    implementation 'commons-io:commons-io:2.11.0'
    compileOnly 'org.spigotmc:spigot:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'com.intellectualsites.fawe:FAWE-Bukkit:1.16-637'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    testCompileOnly 'org.spigotmc:spigot:1.18.1-R0.1-SNAPSHOT:remapped-mojang'
    //testCompileOnly 'org.spigotmc:spigot:1.12.2-R0.1-SNAPSHOT'
}

shadowJar {
    minimize()
    relocate('me.mohamad82.ruom', 'me.mohamad82.pensieve.ruom')
    relocate('org.apache.commons.io', 'me.mohamad82.pensieve.commons.io')
    dependencies {
        exclude(dependency('net.kyori:.*:.*'))
        exclude(dependency('com.zaxxer:HikariCP:.*'))
        exclude(dependency('com.github.cryptomorin:XSeries:.*'))
    }
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}